
SOURCE_ROOT_PWD = $$PWD
TARGET_PWD = $$shadowed($$PWD)

android {
    DEFINES += IS_TARGET_ANDROID ANDROID GLES2
    QMAKE_CXXFLAGS += -std=gnu++11 -pthread
    LIBS += -ldl -lGLESv2 -llog -landroid -lEGL
}

osx:!android {
    DEFINES += IS_TARGET_OSX
    DEFINES += IS_TARGET_APPLE
    QMAKE_CXXFLAGS += -x objective-c++ -std=c++11 -stdlib=libc++
    QMAKE_LFLAGS += -F/System/Library/Frameworks/
    LIBS += -framework AudioUnit -framework Carbon -framework Cocoa -framework CoreAudio -framework ForceFeedback -framework IOKit -framework OpenGL -framework CoreServices
    BLENDER_EXECUTABLE = $$(BLENDER_BIN)/blender.app/Contents/MacOS/blender

}

linux:!android {
    DEFINES += IS_TARGET_LINUX
    BLENDER_EXECUTABLE = $$(BLENDER_BIN)/blender
}

win32:!android {
    DEFINES += IS_TARGET_WINDOWS
    BLENDER_EXECUTABLE = $$(BLENDER_BIN)/blender.exe
}

QMAKE_CXXFLAGS_DEBUG += -D_DEBUG
QMAKE_CXXFLAGS_RELEASE += -O3 -DNDEBUG
QMAKE_LFLAGS_RELEASE -= -O1
QMAKE_LFLAGS_RELEASE += -O3
QMAKE_LFLAGS_DEBUG += -g

defineTest(createDir) {
    dir = $$1
    !exists($$dir) {
        mkpath($$dir)
    }
}

defineTest(addSubdirs) {
    PROJECT_PWD = $$PWD
    TEMPLATE = subdirs
    export(TEMPLATE)
    subdirs = $$files($$PROJECT_PWD/*, false)
    currentProj = $$PROJECT_PWD/$${TARGET}.pro
    for(subdir, subdirs) {
        !equals(currentProj, $$subdir) {
            SUBDIRS += $$basename(subdir)
        }
    }
    #message($$SUBDIRS)
    export (SUBDIRS)
}

defineTest(copyData) {
    PROJECT_PWD = $$PWD
    dir = $$PROJECT_PWD/data
    createDir($$dir)
    osx {
        QMAKE_POST_LINK += cp -R $$dir $$OUT_PWD/$${TARGET}.app/Contents/Resources
    }
    win32 {
        QMAKE_POST_LINK += xcopy $$dir $$OUT_PWD/$${TARGET}
    }
    linux {
        QMAKE_POST_LINK += cp -R $$dir $$OUT_PWD/$${TARGET}
    }
    export(QMAKE_POST_LINK)
}

defineTest(exportBlend) {
    exists($$BLENDER_EXECUTABLE) {
        PROJECT_PWD = $$PWD
        exportScript = $$SOURCE_ROOT_PWD/tools/blenderexport/Export.py
        file = $$1
        fileFullPath = $$PROJECT_PWD/$$file
        name = $$file
        target = $$PROJECT_PWD/$$2/$$basename(fileFullPath).xml
        outputDir = $$PROJECT_PWD/$$2
        createDir($$outputDir)
        eval($${name}.target = $$target)
        export($${name}.target)
        eval($${name}.commands = $$BLENDER_EXECUTABLE $$PROJECT_PWD/$${file}.blend --background --python $$exportScript -- $$outputDir)
        export($${name}.commands)
        QMAKE_EXTRA_TARGETS += $${name}
        POST_TARGETDEPS += $$target
        export(QMAKE_EXTRA_TARGETS)
        export(POST_TARGETDEPS)
    }
}

defineTest(convertTool) {
    !android:!ios {
        PROJECT_PWD = $$PWD
        file = $$PROJECT_PWD/$$1
        outputDir = $$PROJECT_PWD/$$2
        createDir($$outputDir)
        args = $$3
        convertExecutable = $$TARGET_PWD/tools/converter/converter
        name = $$file
        target = $$outputDir/$$basename(name).xml
        eval($${name}.target = $$target)
        export($${name}.target)
        osx {
            eval($${name}.commands = open $${convertExecutable}.app --args -i $$file -o $$outputDir $$args)
        } else {
            eval($${name}.commands = $$convertExecutable -i $$file -o $$outputDir $$args)
        }
        export($${name}.commands)
        QMAKE_EXTRA_TARGETS += $${name}
        POST_TARGETDEPS += $$target
        export(QMAKE_EXTRA_TARGETS)
        export(POST_TARGETDEPS)
    }
}


defineTest(setupSample) {
    PROJECT_PWD = $$PWD
    TEMPLATE = app
    export(TEMPLATE)
    HEADERS += $$files($$PROJECT_PWD/*.h, true)
    export(HEADERS)
    SOURCES += $$files($$PROJECT_PWD/*.cpp, true)
    export(SOURCES)
    INCLUDEPATH += $$files($$SOURCE_ROOT_PWD/NSG/*, false)
    INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/pugixml/src
    INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/imgui
    INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/bullet/src
    export(INCLUDEPATH)
    LIBS += -L../../NSG -lNSG
    LIBS += -L../../dependencies/jpeg -ljpeg
    LIBS += -L../../dependencies/libb64 -llibb64
    LIBS += -L../../dependencies/LZ4 -lLZ4
    LIBS += -L../../externals -lexternals
    export(LIBS)
    OTHER_FILES += $$PROJECT_PWD/data/*
    OTHER_FILES += $$PROJECT_PWD/art/*
    export(OTHER_FILES)
    copyData()
}

defineTest(setupTest) {
    PROJECT_PWD = $$PWD
    TEMPLATE = app
    export(TEMPLATE)
    CONFIG += testcase
    export(CONFIG)
    HEADERS += $$files($$PROJECT_PWD/*.h, true)
    export(HEADERS)
    SOURCES += $$files($$PROJECT_PWD/*.cpp, true)
    export(SOURCES)
    INCLUDEPATH += $$files($$SOURCE_ROOT_PWD/NSG/*, false)
    INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/pugixml/src
    INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/imgui
    INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/bullet/src
    export(INCLUDEPATH)
    LIBS += -L../../NSG -lNSG
    LIBS += -L../../dependencies/jpeg -ljpeg
    LIBS += -L../../dependencies/libb64 -llibb64
    LIBS += -L../../dependencies/LZ4 -lLZ4
    LIBS += -L../../externals -lexternals
    export(LIBS)
    OTHER_FILES += $$PROJECT_PWD/data/*
    OTHER_FILES += $$PROJECT_PWD/art/*
    export(OTHER_FILES)
    copyData()
}

defineTest(setupTool) {
    !android:!ios {
        PROJECT_PWD = $$PWD
        TEMPLATE = app
        export(TEMPLATE)
        HEADERS += $$files($$PROJECT_PWD/*.h, true)
        export(HEADERS)
        SOURCES += $$files($$PROJECT_PWD/*.cpp, true)
        export(SOURCES)
        INCLUDEPATH += $$PROJECT_PWD/../common
        INCLUDEPATH += $$files($$SOURCE_ROOT_PWD/NSG/*, false)
        INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/pugixml/src
        INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/imgui
        INCLUDEPATH += $$SOURCE_ROOT_PWD/externals/bullet/src
        INCLUDEPATH += $$SOURCE_ROOT_PWD/dependencies/tclap/include
        export(INCLUDEPATH)
        LIBS += -L../../tools/common -lcommon
        LIBS += -L../../NSG -lNSG
        LIBS += -L../../dependencies/jpeg -ljpeg
        LIBS += -L../../dependencies/libb64 -llibb64
        LIBS += -L../../dependencies/LZ4 -lLZ4
        LIBS += -L../../externals -lexternals
        export(LIBS)
    }
}


